CMAKE_MINIMUM_REQUIRED(VERSION 3.5)
PROJECT( superray-quadmap )

INCLUDE(GNUInstallDirs)

# COMPILER SETTINGS (default: Release) and flags
SET(CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/CMakeModules")
INCLUDE(CompilerSettings)

# SUPERRAY_QUADMAP_OMP = enable OpenMP parallelization (experimental, defaults to OFF)
SET(SUPERRAY_QUADMAP_OMP FALSE CACHE BOOL "Enable/disable OpenMP parallelization")
IF(DEFINED ENV{SUPERRAY_QUADMAP_OMP})
  SET(SUPERRAY_QUADMAP_OMP $ENV{SUPERRAY_QUADMAP_OMP})
ENDIF(DEFINED ENV{SUPERRAY_QUADMAP_OMP})
IF(SUPERRAY_QUADMAP_OMP)
  FIND_PACKAGE(OpenMP REQUIRED)
  SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${OpenMP_C_FLAGS}")
  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
  SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${OpenMP_EXE_LINKER_FLAGS}")
ENDIF(SUPERRAY_QUADMAP_OMP)

# Set output directories for libraries and executables
SET( BASE_DIR ${CMAKE_SOURCE_DIR} )
SET( CMAKE_LIBRARY_OUTPUT_DIRECTORY ${BASE_DIR}/lib )
SET( CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${BASE_DIR}/lib )
SET( CMAKE_RUNTIME_OUTPUT_DIRECTORY ${BASE_DIR}/bin )
# output dirs for multi-config builds (MSVC)
FOREACH( OUTPUTCONFIG ${CMAKE_CONFIGURATION_TYPES} )
  STRING( TOUPPER ${OUTPUTCONFIG} OUTPUTCONFIG )
  SET( CMAKE_LIBRARY_OUTPUT_DIRECTORY_${OUTPUTCONFIG} ${BASE_DIR}/lib )
  SET( CMAKE_ARCHIVE_OUTPUT_DIRECTORY_${OUTPUTCONFIG} ${BASE_DIR}/lib )
  SET( CMAKE_RUNTIME_OUTPUT_DIRECTORY_${OUTPUTCONFIG} ${BASE_DIR}/bin )
ENDFOREACH( OUTPUTCONFIG CMAKE_CONFIGURATION_TYPES )

SET(INCLUDE_DIRS "${PROJECT_SOURCE_DIR}/include")
INCLUDE_DIRECTORIES(${INCLUDE_DIRS})

LINK_DIRECTORIES(${CMAKE_LIBRARY_OUTPUT_DIRECTORY})

# Installation

SET(INSTALL_TARGETS_DEFAULT_ARGS
  RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}"
  LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
  ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}"
)

ADD_SUBDIRECTORY( src/math )
ADD_SUBDIRECTORY( src )

FILE(GLOB SUPERRAY_QUADMAP_HEADERS ${PROJECT_SOURCE_DIR}/include/superray_quadmap/*.h ${PROJECT_SOURCE_DIR}/include/superray_quadmap/*.hxx)
INSTALL(FILES ${SUPERRAY_QUADMAP_HEADERS} DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/superray_quadmap")
FILE(GLOB SUPERRAY_QUADMATH_HEADERS ${PROJECT_SOURCE_DIR}/include/superray_quadmap/math/*.h)
INSTALL(FILES ${SUPERRAY_QUADMATH_HEADERS} DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}/superray_quadmap/math")

# Install package.xml (catkin/ament/rosdep)
INSTALL(FILES package.xml DESTINATION "${CMAKE_INSTALL_DATADIR}/superray-quadmap")

# Allows Colcon to find non-Ament packages when using workspace underlays
FILE(WRITE ${CMAKE_CURRENT_BINARY_DIR}/share/ament_index/resource_index/packages/${PROJECT_NAME} "")
INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/share/ament_index/resource_index/packages/${PROJECT_NAME} DESTINATION share/ament_index/resource_index/packages)

# uninstall target
IF(NOT TARGET uninstall)
  CONFIGURE_FILE(
    "${PROJECT_SOURCE_DIR}/CMakeModules/CMakeUninstall.cmake.in"
    "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake"
    IMMEDIATE @ONLY
  )

  ADD_CUSTOM_TARGET(uninstall
    COMMAND ${CMAKE_COMMAND} -P ${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake
  )
ENDIF()

# Export the package for use from the build-tree
# (this registers the build-tree with a global CMake-registry)
EXPORT(PACKAGE superray-quadmap)
 
# Create a superray-quadmap-config.cmake file for the use from the build tree
SET(SUPERRAY_QUADMAP_INCLUDE_DIRS "${INCLUDE_DIRS}")
SET(SUPERRAY_QUADMAP_LIB_DIR "${CMAKE_LIBRARY_OUTPUT_DIRECTORY}")
# Set library names as absolute paths
# Windows, spec. MSVC requires the .lib suffix for imported libs
IF(WIN32)
  SET(SUPERRAY_QUADMAP_LIBRARY "${CMAKE_IMPORT_LIBRARY_PREFIX}superray-quadmap${CMAKE_IMPORT_LIBRARY_SUFFIX}")
  SET(SUPERRAY_QUADMATH_LIBRARY "${CMAKE_IMPORT_LIBRARY_PREFIX}superray-quadmath${CMAKE_IMPORT_LIBRARY_SUFFIX}")
ELSE()
  SET(SUPERRAY_QUADMAP_LIBRARY "${CMAKE_SHARED_LIBRARY_PREFIX}superray-quadmap${CMAKE_SHARED_LIBRARY_SUFFIX}")
  SET(SUPERRAY_QUADMATH_LIBRARY "${CMAKE_SHARED_LIBRARY_PREFIX}superray-quadmath${CMAKE_SHARED_LIBRARY_SUFFIX}")
ENDIF()

INCLUDE(CMakePackageConfigHelpers)

CONFIGURE_PACKAGE_CONFIG_FILE(
  superray-quadmap-config.cmake.in
  "${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/cmake/superray-quadmap/superray-quadmap-config.cmake"
  PATH_VARS SUPERRAY_QUADMAP_INCLUDE_DIRS SUPERRAY_QUADMAP_LIB_DIR
  INSTALL_DESTINATION "${CMAKE_INSTALL_FULL_DATADIR}/superray-quadmap"
)

# Create a superray-quadmap-config.cmake file for the use from the install tree and install it
SET(SUPERRAY_QUADMAP_INCLUDE_DIRS "${CMAKE_INSTALL_FULL_INCLUDEDIR}")
SET(SUPERRAY_QUADMAP_LIB_DIR "${CMAKE_INSTALL_FULL_LIBDIR}")

SET(SUPERRAY_QUADMAP_INCLUDE_TARGETS
  "include(\${CMAKE_CURRENT_LIST_DIR}/superray-quadmap-targets.cmake)")

CONFIGURE_PACKAGE_CONFIG_FILE(
  superray-quadmap-config.cmake.in
  "${PROJECT_BINARY_DIR}/InstallFiles/superray-quadmap-config.cmake"
  PATH_VARS SUPERRAY_QUADMAP_INCLUDE_DIRS SUPERRAY_QUADMAP_LIB_DIR
  INSTALL_DESTINATION "${CMAKE_INSTALL_FULL_DATADIR}/superray-quadmap"
)

INSTALL(FILES
  "${PROJECT_BINARY_DIR}/InstallFiles/superray-quadmap-config.cmake"
  DESTINATION "${CMAKE_INSTALL_DATADIR}/superray-quadmap"
)

# Finished:
MESSAGE (STATUS "\n")
MESSAGE (STATUS "Compile superray-quadmap using: make")
MESSAGE (STATUS "Install superray-quadmap using: make install")
MESSAGE (STATUS "    (be sure to set the correct CMAKE_INSTALL_PREFIX before)")
